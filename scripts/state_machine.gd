extends Node

@export var initial_state : CatState

var current_state: CatState
var states: Dictionary = {}


func _ready() -> void:
	for child in get_children():
		if child is CatState:
			var child_name = child.name.to_lower()
			child.set_state_name(child_name)
			states[child_name] = child
			child.Transitioned.connect(on_child_transition)
	
	if initial_state:
		initial_state.Enter()
		current_state = initial_state


func _process(delta: float) -> void:
	if current_state:
		current_state.Update(delta)
	
	
func _physics_process(delta: float) -> void:
	if current_state:
		current_state.Physics_update(delta)


func on_child_transition(state, new_state_name):
	if state != current_state:
		return
		
	var new_state = states.get(new_state_name.to_lower())
	if !new_state:
		return
		
	if current_state:
		current_state.Exit()
	
	new_state.Enter()
	current_state = new_state
